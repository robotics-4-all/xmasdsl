XmasModel:
    imports*=Import
    (metadata=Metadata)?
    (rangeDefs*=RangeDef)?
    (colorDefs*=ColorDef)?
    (groupDefs*=CommandGroup)?
    program=Program
;

Metadata:
    'Metadata'
    (
        ('name:' name=STRING)
        ('description:' description=STRING)?
        ('author:' author=STRING)?
        ('email:' email=STRING)?
        ('token:' token=STRING)?
    )#
    'end'
;

ColorDef:
    'Color' name=ID color=Color
;

RangeDef:
    'Range' name=ID range=Ranges
;

Range:
    start=INT (hasEnd=':' (end=INT)?)? (hasStep=':' (step=ChangeIntBy)?)?
;

Ranges:
    '[' ranges*=Range[','] ']'
;

Color:
    '[' r=ChangeIntBy  ',' g=ChangeIntBy  ',' b=ChangeIntBy ']'
;

ChangeIntBy:
    (changeBy=ChangeBy)? val=INT
;


Command:
    SetPixelColor   |
    Dim				|
    Delay           |
    SetBrightness
;

GroupRef:
    ref=[CommandGroup]
;

SetPixelColor:
    'SetPixelColor' '(' pixels=[RangeDef] ',' color=[ColorDef] ',' duration=INT ')'
;

Dim:
    'Dim' '(' range=[RangeDef] ',' brStart=INT ',' brEnd=INT ',' duration=INT ')'
;

Delay:
    'Delay' '(' duration=ChangeIntBy ')'
;

SetBrightness:
    'SetBrightness' '(' value=ChangeIntBy ')'
;

Process:
    Serial          |
    Parallel        |
    GroupRef
;

ChangeBy:
    '+' |
    '-'
;

SerCommand:
    Command | Parallel | Repeat | GroupRef
;

ProgramCommand: Command | Parallel | Repeat | GroupRef;

Program:
'Program' '{'
(
    (commands*=ProgramCommand)?
)#
'}'
;

Serial:
'Serial' '{'
(
    (commands*=SerCommand)?
)#
'}'
;

CommandGroup:
'Group' name=ID '{'
(
    (commands*=SerCommand)?
)#
'}'
;

Parallel:
'Parallel' '{'
(
    (processes*=Process)?
)#
'}'
;

Repeat:
'Repeat' times=INT '{'
(
    (commands*=SerCommand)?
)#
'}'
;

// FQN: ID('.'ID)*;
FQN: ID+['.'];
FQNI: ID+['.']('.*')?;
// Import: 'import' importURI=FQNI ('as' name=ID)?;
NID: /(.*?)\n/;
NIDREF: NID+['.']('.*')?;
Import: 'import' name=STRING;

// Comments
Comment: CommentLine | CommentBlock ;
CommentLine: /\/\/.*?$/;
CommentBlock: /\/\*(.|\n)*?\*\//;
