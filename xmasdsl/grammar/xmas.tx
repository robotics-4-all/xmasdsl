XmasModel:
    imports*=Import
    (metadata=Metadata)?
    (rangeDefs*=RangeDef)?
    (colorDefs*=ColorDef)?
    (groupDefs*=CommandGroup)?
    program=Program
;

Metadata:
    'Metadata'
    (
        ('name:' name=STRING)
        ('description:' description=STRING)?
        ('author:' author=STRING)?
        ('email:' email=STRING)?
        ('token:' token=STRING)?
    )#
    'end'
;

ColorDef:
    'Color' name=ID color=Color
;

RangeDef:
    'Range' name=ID range=Ranges
;

Range:
    from=INT (hasTo='-' (to=INT)?)?
;

Ranges:
    '[' ranges*=Range[','] ']'
;

Color:
    '[' r=INT  ',' g=INT  ',' b=INT ']'
;


Command:
    SetPixelColor   |
    Delay           |
    SetBrightness
;

GroupRef:
    ref=[CommandGroup]
;

SetPixelColor:
    'SetPixelColor' '(' pixels=[RangeDef] ',' color=[ColorDef] ',' duration=INT ')'
;

Delay:
    'Delay' '(' duration=INT ')'
;

SetBrightness:
    'SetBrightness' '(' value=INT ')'
;

Process:
    Serial          |
    Parallel        |
    GroupRef
;

SerCommand:
    Command | Parallel | GroupRef
;

ProgramCommand: Command | Parallel| GroupRef;

Program:
'Program' '{'
(
    (commands*=ProgramCommand)?
)#
'}'
;

Serial:
'Serial' '{'
(
    (commands*=SerCommand)?
)#
'}'
;

CommandGroup:
'Group' name=ID '{'
(
    (commands*=SerCommand)?
)#
'}'
;

Parallel:
'Parallel' '{'
(
    (processes*=Process)?
)#
'}'
;

// FQN: ID('.'ID)*;
FQN: ID+['.'];
FQNI: ID+['.']('.*')?;
// Import: 'import' importURI=FQNI ('as' name=ID)?;
NID: /(.*?)\n/;
NIDREF: NID+['.']('.*')?;
Import: 'import' name=STRING;

// Comments
Comment: CommentLine | CommentBlock ;
CommentLine: /\/\/.*?$/;
CommentBlock: /\/\*(.|\n)*?\*\//;
