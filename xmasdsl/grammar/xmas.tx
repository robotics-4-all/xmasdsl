import common

XmasModel:
    imports*=Import
	(
		(metadata=Metadata)?
		(randomRangeDefs*=RandomRangeDef)
		(randomColorDefs*=RandomColorDef)
		(rangeDefs*=RangeDef)
		(colorDefs*=ColorDef)
		(groupDefs*=CommandGroup)
		(program=Program)
	)#
;

Metadata:
    'Metadata'
    (
        ('name:' name=STRING)
        ('description:' description=STRING)?
        ('author:' author=STRING)?
        ('email:' email=STRING)?
        ('token:' token=STRING)?
    )#
    'end'
;

Color:
    '[' r=ChangeIntBy  ',' g=ChangeIntBy  ',' b=ChangeIntBy ']'
;

ColorDef:
    'Color' name=ID color=Color
;

CDef:
    ColorDef |
    RandomColorDef
;

ColorArgument:
    (colorDef=[CDef:FQN] | color=Color)
;

RandomColorArgument:
    (colorDef=[ColorDef] | color=Color)
;

RandomColorDef:
    'RandomColor' name=ID '(' colorA=RandomColorArgument ',' colorB=RandomColorArgument ')'
;

RangeDef:
    'Range' name=ID range=Ranges
;

RandomRangeDef:
    'RandomRange' name=ID '([' range=Range '],' percentage=INT ')'
;

Range:
    start=INT (hasEnd=':' (end=INT)?)? (hasStep=':' (step=ChangeIntBy)?)?
;

Ranges:
    '[' ranges*=Range[','] ']'
;

RangeType:
    RangeDef | Ranges
;

ChangeIntBy:
    (changeBy=ChangeBy)? val=INT
;


Command:
    SetPixelColor   |
    Dim				|
	Rainbow			|
	Linear			|
    Delay           |
    SetBrightness
;

GroupRef:
    ref=[CommandGroup]
;

RDef:
    RangeDef |
    RandomRangeDef
;


RangeArgument:
    (rangeDef=[RDef:FQN] | range=Ranges)
;


SetPixelColor:
    'SetPixelColor' '(' (range=RangeArgument) ',' color=ColorArgument ','
        duration=INT (',' maintain=BOOL)? ')'
;

Dim:
    'Dim' '(' (range=RangeArgument) ',' (color=ColorArgument) ',' duration=INT
		(',' fadeIn=BOOL)? ')'
;

Rainbow:
    'Rainbow' '(' (range=RangeArgument) ',' colorStart=ColorArgument ','
        colorEnd=ColorArgument ',' duration=INT (',' maintain=BOOL)? ')'
;

Linear:
    'Linear' '(' (rangeStart=RangeArgument) ',' (rangeEnd=RangeArgument) ','
        colorStart=ColorArgument ',' colorEnd=ColorArgument ',' duration=INT
		(',' maintain=BOOL)? ')'
;

Delay:
    'Delay' '(' duration=ChangeIntBy ')'
;

SetBrightness:
    'SetBrightness' '(' value=ChangeIntBy (',' (range=RangeArgument))? ')'
;

Process:
    Serial          |
    Parallel        |
    GroupRef
;

ChangeBy:
    '+' |
    '-'
;

SerCommand:
    Command | Parallel | Repeat | GroupRef
;

ProgramCommand: Command | Parallel | Repeat | GroupRef;

Program:
'Program' '{'
(
    (commands*=ProgramCommand)?
)#
'}'
;

Serial:
'Serial' '{'
(
    (commands*=SerCommand)?
)#
'}'
;

CommandGroup:
'Group' name=ID '{'
(
    (commands*=SerCommand)?
)#
'}'
;

Parallel:
'Parallel' '{'
(
    (processes*=Process)?
)#
'}'
;

Repeat:
'Repeat' times=INT '{'
(
    (commands*=SerCommand)?
)#
'}'
;
