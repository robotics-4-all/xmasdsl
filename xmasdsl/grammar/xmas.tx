import common

XmasModel:
    imports*=Import
	(
		(metadata=Metadata)?
		(randomRangeDefs*=RandomRangeDef)
		(rangeDefs*=RangeDef)
		(colorDefs*=ColorDef)
		(groupDefs*=CommandGroup)
		(program=Program)
	)#
;

Metadata:
    'Metadata'
    (
        ('name:' name=STRING)
        ('description:' description=STRING)?
        ('author:' author=STRING)?
        ('email:' email=STRING)?
        ('token:' token=STRING)?
    )#
    'end'
;

ColorDef:
    'Color' name=ID color=Color
;

RangeDef:
    'Range' name=ID range=Ranges
;

RandomRangeDef:
    'RandomRange' name=ID '([' range=Range '],' percentage=INT ')'
;

Range:
    start=INT (hasEnd=':' (end=INT)?)? (hasStep=':' (step=ChangeIntBy)?)?
;

Ranges:
    '[' ranges*=Range[','] ']'
;

RangeType:
    RangeDef | Ranges
;

Color:
    '[' r=ChangeIntBy  ',' g=ChangeIntBy  ',' b=ChangeIntBy ']'
;

ChangeIntBy:
    (changeBy=ChangeBy)? val=INT
;


Command:
    SetPixelColor   |
    Dim				|
	ColorTransition |
	Linear			|
    Delay           |
    SetBrightness
;

GroupRef:
    ref=[CommandGroup]
;


RangeArgument:
    (rangeDef=[RangeDef:FQN] | range=Ranges | randRange=[RandomRangeDef:FQN])
;


SetPixelColor:
    'SetPixelColor' '(' (range=RangeArgument) ',' color=[ColorDef] ','
        duration=INT (',' maintain=BOOL)? ')'
;

Dim:
    'Dim' '(' (range=RangeArgument) ',' brStart=INT ',' brEnd=INT ','
        duration=INT ')'
;

ColorTransition:
    'ColorTransition' '(' (range=RangeArgument) ',' colorStart=[ColorDef] ','
        colorEnd=[ColorDef] ',' duration=INT ')'
;

Linear:
    'Linear' '(' (range=RangeArgument) ',' (offset=RangeArgument) ','
        colorStart=[ColorDef] ',' colorEnd=[ColorDef] ',' duration=INT ','
        maintain=BOOL ')'
;

Delay:
    'Delay' '(' duration=ChangeIntBy ')'
;

SetBrightness:
    'SetBrightness' '(' value=ChangeIntBy (',' range=RangeArgument)? ')'
;

Process:
    Serial          |
    Parallel        |
    GroupRef
;

ChangeBy:
    '+' |
    '-'
;

SerCommand:
    Command | Parallel | Repeat | GroupRef
;

ProgramCommand: Command | Parallel | Repeat | GroupRef;

Program:
'Program' '{'
(
    (commands*=ProgramCommand)?
)#
'}'
;

Serial:
'Serial' '{'
(
    (commands*=SerCommand)?
)#
'}'
;

CommandGroup:
'Group' name=ID '{'
(
    (commands*=SerCommand)?
)#
'}'
;

Parallel:
'Parallel' '{'
(
    (processes*=Process)?
)#
'}'
;

Repeat:
'Repeat' times=INT '{'
(
    (commands*=SerCommand)?
)#
'}'
;
